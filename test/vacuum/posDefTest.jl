#= Asym(G0) is positive semi-definite in vacuum, exactly at real frequency and
strictly at complex frequency, so its worst eigenvalue measures the whole build.
Normalized by lamMax, the bar is a multiple of eps rather than an absolute
number that hides behind a small operator. =#
using Test, GilaElectromagnetics, LinearAlgebra

function pdAsyEig(cel, scl, frq)
    opt = CPUKerOpt{Float64}()
    opt.frqPhz = frq
    mat = dnsMat(GlaVacOprMem(opt, GlaVol(cel, scl, stdOrg)))
    return eigvals(Hermitian((mat - adjoint(mat)) / 2im))
end

@testset "Positive Semi-Definiteness Tests" begin
    sclSlv = (1//32, 1//32, 1//512)

    @testset "cubic cells" begin
        for cel ∈ volSizes
            ev = pdAsyEig(cel, stdScl, 1.0 + 0.0im)
            rat = minimum(ev) / maximum(ev)
            @info "Asym(G0) $cel f = 1: lamMin / lamMax = $rat"
            @test rat > -1e-12
            @test minimum(pdAsyEig(cel, stdScl, 1.0 + 0.1im)) > 0
        end
    end

    #= The 1:1:16 cell, the shape both halves of the old build were wrong on,
    in opposite directions. =#
    @testset "slender cells" begin
        ev = pdAsyEig((6, 6, 12), sclSlv, 1.0 + 0.0im)
        rat = minimum(ev) / maximum(ev)
        @info "Asym(G0) slender f = 1: lamMin / lamMax = $rat"
        @test rat > -1e-12
        lam = minimum(pdAsyEig((6, 6, 12), sclSlv, 1.0 + 0.1im))
        @info "Asym(G0) slender f = 1 + 0.1i: lamMin = $lam"
        @test lam > 0
    end
end
